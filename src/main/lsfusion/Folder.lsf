MODULE Folder;

REQUIRE Email, Config;

NAMESPACE Observer; 

CLASS Folder 'Folder';
TABLE folder(Folder);

dateTime 'Created' = DATA DATETIME (Folder) CHARWIDTH 20;
name 'Name' = DATA STRING[200] (Folder) NONULL;

folder 'Folder' = GROUP AGGR Folder p BY name(p);

// Extend the Account class of the Platform
folder 'Folder' = DATA Folder (Account);
nameFolder 'Folder' (Account m) = name(folder(m)) CHARWIDTH 30;

// Forms
showDelete 'Show deletion button' = DATA LOCAL BOOLEAN ();

FORM folder 'Edit Folder'
    OBJECTS p = Folder PANEL 
    PROPERTIES (p) name
    
    OBJECTS a = Account 
    PROPERTIES (a) name, NEW , DELETE 
    FILTERS folder(a) == p
    
    PROPERTIES showDelete()
    EDIT Folder OBJECT p
;

DESIGN folder { 
    NEW pane FIRST {
        type = CONTAINERH;
        alignment = STRETCH;
        fill = 1;
        NEW folder {
            type = CONTAINERV;    
            alignment = STRETCH;
            width = 500;
            MOVE PROPERTY(name(p)) {alignment = STRETCH; panelCaptionVertical = TRUE; } 
            MOVE BOX (a) { alignment = STRETCH; fill = 1; }
        }
    }
    TOOLBAR {
        MOVE PROPERTY (showDelete());
    }
}

FORM folders 'Folders'
    OBJECTS p = Folder
    PROPERTIES (p) READONLY name
    PROPERTIES (p) NEWSESSION NEW , EDIT , DELETE 
    
    OBJECTS a = Account
    PROPERTIES (a) name
    FILTERS folder(a) == p
;

NAVIGATOR {
    NEW folders FIRST;
}
