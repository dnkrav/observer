MODULE Runner;

REQUIRE Itemail;

NAMESPACE Observer;

// Block for single import process running
isBlocked 'Import process is busy' () = DATA BOOLEAN ();

// Import data from MBOX format
mboxImporter 'Import data from MBOX archive' INTERNAL 'lsfusion.solutions.observer.MboxImporter' (Mbox);

// Read MIME Headers
headerReader 'Read MIME Headers' INTERNAL 'lsfusion.solutions.observer.HeaderReader' (Itemail);

// Runner for MBOX import Scheduler
lastImporterRun 'Name of the last processed MBOX archive' = DATA STRING ();
lastImporterResult 'Result of the last run' = DATA STRING[10] ();
lastImporterOutput 'Output from the last run' = DATA TEXT ();

importNextFile 'Import from next MBOX archive' () {
    LOCAL nextFile = Mbox(); 
    nextFile() <- GROUP LAST Mbox v IF toImport(v) AND NOT isImported(v) ORDER filename(v);
    
    IF NOT nextFile() THEN RETURN;
    
    IF NOT filelink(nextFile()) OR NOT filelinkOut(nextFile()) THEN {
        printToLog('Cannot import from MBOX archive, because some of links empty: ' + (OVERRIDE filelink(nextFile()),filelinkOut(nextFile()),filename(nextFile()),'unknown'));
        toImport(nextFile()) <- NULL;
        RETURN;
    }
    
    IF isBlocked() THEN {
        printToLog('Import is blocked by another process');
        RETURN;
    }
    
    IF lastImporterResult() != 'Success' THEN {
        printToLog('Import was not started, because the last Import Result requires action of the Admininstrator');
        RETURN;
    }
    
    NEWSESSION { 
        lastImporterResult() <- 'Stop';
        lastImporterOutput() <- NULL;
        isBlocked() <- TRUE; 
        APPLY; 
    }
    
    printToLog('Start importing from MBOX archive: ' + filelink(nextFile()));
    fileExists(filelink(nextFile()));
    IF NOT fileExists() THEN {
        lastImporterOutput() <- 'Import was not started, because the next file does not exist ' + filelink(nextFile());
        printToLog(lastImporterOutput());
        isBlocked() <- NULL;
        APPLY;
        RETURN;
    }
    
    // Main Run with logging
    FOR Mbox m AND m == nextFile() NEW l = MboxLog DO {
        mbox(l) <- m;
        start(l) <- currentDateTime();
        
        LOCAL initial = INTEGER ();
        initial() <- imported(m); 
        TRY {
        
            mboxImporter(m);
        
            output(l) <- lastImporterOutput();
        }
        CATCH {
            printToLog('Import Run ended with exception: ' + messageCaughtException());
            output(l) <- messageCaughtException();
        }
        
        result(l) <- lastImporterResult();
        lines(l) <- progress(m);
        added(l) <- OVERRIDE (imported(m) - initial()), imported(m), 0;
        finish(l) <- currentDateTime();
    }
    
    
    printToLog('Stop importing from MBOX archive: ' + filelink(nextFile()));
    printToLog(lastImporterOutput());
    isBlocked() <- NULL;
    APPLY;
}

// Process monitor
FORM importer 'MBOX Import'
    OBJECTS m = Mbox
    PROPERTIES (m) filename, path, toImport, isImported, wrongFormat, length, progress, messages, imported
    
    OBJECTS l = MboxLog
    PROPERTIES (l) READONLY start, finish, result, email, filname, length, messages, lines, added, output PANEL
    
    PROPERTIES() PANEL isBlocked, importNextFile TOOLBAR DRAW m, lastImporterRun, lastImporterResult, scrollOutput = lastImporterOutput
;

DESIGN importer { 
    NEW pane FIRST { 
        fill = 1;
        alignment = STRETCH;
        MOVE BOX (m);
        MOVE BOX (l);
        NEW details {
            type = CONTAINERH;
            alignment = STRETCH;
            height = 200;
            MOVE PROPERTY (output(l)) { alignment = STRETCH; fill = 1; panelCaptionVertical = TRUE; }
            NEW lastRun {
                caption = 'Last Importer Run';
                alignment = STRETCH;
                fill = 1;
                MOVE PROPERTY (isBlocked());
                MOVE PROPERTY (lastImporterRun()) { alignment = STRETCH; }
                MOVE PROPERTY (lastImporterResult()) { alignment = STRETCH; }
            }
        }
    }
}
@scrollBarView(importer, lastRun, scrollOutput, 'Output of the last run');

NAVIGATOR {
    NEW importer BEFORE masterData;
}

EXTEND FORM parameters 
    PROPERTIES () numberToImport, isBlocked, lastImporterResult
    PROPERTIES () READONLY lastImporterRun, lastImporterOutput
;
DESIGN parameters { pane { 
    NEW process {
        caption = 'Process monitor';
        MOVE PROPERTY (numberToImport()); 
        MOVE PROPERTY (isBlocked());
    }
    NEW lastRun {
        caption = 'Last Importer Run';
        alignment = STRETCH;
        MOVE PROPERTY (lastImporterRun()) { alignment = STRETCH; }
        MOVE PROPERTY (lastImporterResult()) { alignment = STRETCH; }
        MOVE PROPERTY (lastImporterOutput()) { fill = 1;}
    }
}}

EXTEND FORM folders
    PROPERTIES (m) SHOWIF NOT converted(m) TOOLBAR headerReader
;